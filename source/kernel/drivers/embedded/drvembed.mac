    ; Embedded ROM DISK driver for Nextor 2.0
    ; By Konamiman, 8/2018
    ;
    ; Provides a read only disk image

	include		condasm.inc
	include		bluemsx.inc
	include		const.inc

SECCNT	EQU	30	; NUMBER OF SECTORS STORED IN EACH BANK
SECSIZ	EQU	512	; NUMBER OF BYTES PER SECTOR
SECTOT	EQU	16*SECCNT ; MUST BE LESS THAN 255


;-----------------------------------------------------------------------------
;
; Standard BIOS and work area entries

CHPUT	equ	00A2h	;Character output
CHGET	equ	009Fh


DRV_START:

;-----------------------------------------------------------------------------
;
; Miscellaneous constants
;

;This is a 2 byte buffer to store the address of code to be executed.
;It is used by some of the kernel page 0 routines.

CODE_ADD	equ	0F84Ch


;-----------------------------------------------------------------------------
;
; Driver configuration constants
;

;Driver type:
;   for device-based

;DRV_TYPE	equ	1

;Driver version

VER_MAIN	equ	1
VER_SEC		equ	0
VER_REV		equ	0


;-----------------------------------------------------------------------------
;
; Error codes for DEV_RW
;


.NCOMP	equ	0FFh
.WRERR	equ	0FEh
.DISK	equ	0FDh
.NRDY	equ	0FCh
.DATA	equ	0FAh
.RNF	equ	0F9h
.WPROT	equ	0F8h
.UFORM	equ	0F7h
.SEEK	equ	0F3h
.IFORM	equ	0F0h
.IDEVL	equ	0B5h
.IPARM	equ	08Bh



;-----------------------------------------------------------------------------
;
; Routines and information available on kernel page 0
;

;* Get in A the current slot for page 1. Corrupts F.
;  Must be called by using CALBNK to bank 0:
;    xor a
;    ld ix,GSLOT1
;    call CALBNK

GSLOT1	equ	402Dh


;* This routine reads a byte from another bank.
;  Must be called by using CALBNK to the desired bank,
;  passing the address to be read in HL:
;    ld a,<bank number>
;    ld hl,<byte address>
;    ld ix,RDBANK
;    call CALBNK

RDBANK	equ	403Ch


;* This routine temporarily switches kernel main bank
;  (usually bank 0, but will be 3 when running in MSX-DOS 1 mode),
;  then invokes the routine whose address is at (CODE_ADD).
;  It is necessary to use this routine to invoke CALBAS
;  (so that kernel bank is correct in case of BASIC error)
;  and to invoke DOS functions via F37Dh hook.
;
;  Input:  Address of code to invoke in (CODE_ADD).
;          AF, BC, DE, HL, IX, IY passed to the called routine.
;  Output: AF, BC, DE, HL, IX, IY returned from the called routine.

CALLB0	equ	403Fh


;* Call a routine in another bank.
;  Must be used if the driver spawns across more than one bank.
;
;  Input:  A = bank number
;          IX = routine address
;          AF' = AF for the routine
;          HL' = Ix for the routine
;          BC, DE, HL, IY = input for the routine
;  Output: AF, BC, DE, HL, IX, IY returned from the called routine.

CALBNK	equ	4042h


;* Get in IX the address of the SLTWRK entry for the slot passed in A,
;  which will in turn contain a pointer to the allocated page 3
;  work area for that slot (0 if no work area was allocated).
;  If A=0, then it uses the slot currently switched in page 1.
;  Returns A=current slot for page 1, if A=0 was passed.
;  Corrupts F.
;  Must be called by using CALBNK to bank 0:
;    ld a,<slot number> (xor a for current page 1 slot)
;    ex af,af'
;    xor a
;    ld ix,GWORK
;    call CALBNK

GWORK	equ	4045h


;* This address contains one byte that tells how many banks
;  form the Nextor kernel (or alternatively, the first bank
;  number of the driver).

K_SIZE	equ	40FEh


;* This address contains one byte with the current bank number.

CUR_BANK	equ	40FFh


;-----------------------------------------------------------------------------
;
; Built-in format choice strings
;

NULL_MSG  equ     781Fh	;Null string (disk can't be formatted)
SING_DBL  equ     7820h ;"1-Single side / 2-Double side"


;-----------------------------------------------------------------------------
;
; Driver signature
;
	db	"NEXTOR_DRIVER",0


;-----------------------------------------------------------------------------
;
; Driver flags:
;    bit 0: 0 for drive-based, 1 for device-based
;    bit 2: 1 if the driver implements the DRV_CONFIG routine
;             (used by Nextor from v2.0.5)

	db	1+4


;-----------------------------------------------------------------------------
;
; Reserved byte
;

	db	0


;-----------------------------------------------------------------------------
;
; Driver name
;

DRV_NAME:
	db	"Embedded ROM DISK Driver"
	ds	32-($-DRV_NAME)," "


;-----------------------------------------------------------------------------
;
; Jump table for the driver public routines
;

	; These routines are mandatory for all drivers
        ; (but probably you need to implement only DRV_INIT)

	jp	DRV_TIMI
	jp	DRV_VERSION
	jp	DRV_INIT
	jp	DRV_BASSTAT
	jp	DRV_BASDEV
	jp	DRV_EXTBIO
	jp	DRV_DIRECT0
	jp	DRV_DIRECT1
	jp	DRV_DIRECT2
	jp	DRV_DIRECT3
	jp	DRV_DIRECT4
	jp	DRV_CONFIG

	ds	12

	; These routines are mandatory for device-based drivers

	jp	DEV_RW
	jp	DEV_INFO
	jp	DEV_STATUS
	jp	LUN_INFO


;=====
;=====  END of data that must be at fixed addresses
;=====


;-----------------------------------------------------------------------------
;
; Timer interrupt routine, it will be called on each timer interrupt
; (at 50 or 60Hz), but only if DRV_INIT returns Cy=1 on its first execution.

DRV_TIMI:
	ret


;-----------------------------------------------------------------------------
;
; Driver initialization routine, it is called twice:
;
; 1) First execution, for information gathering.
;    Input:
;      A = 0
;      B = number of available drives
;      HL = maximum size of allocatable work area in page 3
;    Output:
;      A = number of required drives (for drive-based driver only)
;      HL = size of required work area in page 3
;      Cy = 1 if DRV_TIMI must be hooked to the timer interrupt, 0 otherwise
;
; 2) Second execution, for work area and hardware initialization.
;    Input:
;      A = 1
;      B = number of allocated drives for this controller
;
;    The work area address can be obtained by using GWORK.
;
;    If first execution requests more work area than available,
;    second execution will not be done and DRV_TIMI will not be hooked
;    to the timer interrupt.
;
;    If first execution requests more drives than available,
;    as many drives as possible will be allocated, and the initialization
;    procedure will continue the normal way
;    (for drive-based drivers only. Device-based drivers always
;     get two allocated drives.)

DRV_INIT:
	or	a
	ld	hl,0
	ld	a,2
	ret	z			;Note that Cy is 0 (no interrupt hooking needed)

	ld	de,INFO_S
	call	PRINT

	xor	a
	ld	hl,0
	ret


;-----------------------------------------------------------------------------
;
; Obtain driver version
;
; Input:  -
; Output: A = Main version number
;         B = Secondary version number
;         C = Revision number

DRV_VERSION:
	ld	a,VER_MAIN
	ld	b,VER_SEC
	ld	c,VER_REV
	ret


;-----------------------------------------------------------------------------
;
; BASIC expanded statement ("CALL") handler.
; Works the expected way, except that if invoking CALBAS is needed,
; it must be done via the CALLB0 routine in kernel page 0.

DRV_BASSTAT:
	scf
	ret


;-----------------------------------------------------------------------------
;
; BASIC expanded device handler.
; Works the expected way, except that if invoking CALBAS is needed,
; it must be done via the CALLB0 routine in kernel page 0.

DRV_BASDEV:
	scf
	ret


;-----------------------------------------------------------------------------
;
; Extended BIOS hook.
; Works the expected way, except that it must return
; D'=1 if the old hook must be called, D'=0 otherwise.
; It is entered with D'=1.

DRV_EXTBIO:
	ret


;-----------------------------------------------------------------------------
;
; Direct calls entry points.
; Calls to addresses 7850h, 7853h, 7856h, 7859h and 785Ch
; in kernel banks 0 and 3 will be redirected
; to DIRECT0/1/2/3/4 respectively.
; Receives all register data from the caller except IX and AF'.

DRV_DIRECT0:
DRV_DIRECT1:
DRV_DIRECT2:
DRV_DIRECT3:
DRV_DIRECT4:
	ret


;-----------------------------------------------------------------------------
;
; Get driver configuration
; (bit 2 of driver flags must be set if this routine is implemented)
;
; Input:
;   A = Configuration index
;   BC, DE, HL = Depends on the configuration
;
; Output:
;   A = 0: Ok
;       1: Configuration not available for the supplied index
;   BC, DE, HL = Depends on the configuration
;
; * Get number of drives at boot time (for device-based drivers only):
;   Input:
;     A = 1
;     B = 0 for DOS 2 mode, 1 for DOS 1 mode
;   Output:
;     B = number of drives
;
; * Get default configuration for drive
;   Input:
;     A = 2
;     B = 0 for DOS 2 mode, 1 for DOS 1 mode
;     C = Relative drive number at boot time
;   Output:
;     B = Device index
;     C = LUN index

DRV_CONFIG:
	CP	1
	JR	NZ, DRV_CONFIG1
	LD	B, 2
	XOR	A
	RET

DRV_CONFIG1:
	CP	2
	JR	NZ, DRV_CONFIG2
	LD	B, 1
	LD	C, 1
	XOR	A
	RET

DRV_CONFIG2:
	LD	A, 1
	RET


;=====
;=====  BEGIN of DEVICE-BASED specific routines
;=====

;-----------------------------------------------------------------------------
;
; Read or write logical sectors from/to a logical unit
;
;Input:    Cy=0 to read, 1 to write
;          A = Device number, 1 to 7
;          B = Number of sectors to read or write
;          C = Logical unit number, 1 to 7
;          HL = Source or destination memory address for the transfer
;          DE = Address where the 4 byte sector number is stored.
;Output:   A = Error code (the same codes of MSX-DOS are used):
;              0: Ok
;              .IDEVL: Invalid device or LUN
;              .NRDY: Not ready
;              .DISK: General unknown disk error
;              .DATA: CRC error when reading
;              .RNF: Sector not found
;              .UFORM: Unformatted disk
;              .WPROT: Write protected media, or read-only logical unit
;              .WRERR: Write error
;              .NCOMP: Incompatible disk.
;              .SEEK: Seek error.
;          B = Number of sectors actually read (in case of error only)

ST_RD_CNT	EQU	0	; COUNT OF SECTORS TO BE READ
ST_INDX_LW	EQU	1	; SECTOR TO BE READ
ST_INDX_HI	EQU	2	; SECTOR TO BE READ 	; MUST BE LESS THAN 65536
ST_DEST_LW	EQU	3	; SECTOR DESTINATION ADDRESS (LOW)
ST_DEST_HI	EQU	4	; (HIGH)
ST_RD_CND_RQ	EQU	5	; NUMBER OF SECTORS REQUESTED

DEV_RW:
	JR	NC, DEV_DO_RD		; WE ONLY DO READS

	LD	A, .WPROT
	LD	B, 0
	RET

DEV_DO_RD:
	CALL	MY_GWORK

	LD	(IX+ST_RD_CNT), B
	LD	(IX+ST_RD_CND_RQ), B
	LD	A, (DE)			; RETRIEVE SECTOR NUMBER  (ASSUMING LESS THEN 255 FOR MOMENT)
	LD	(IX+ST_INDX_LW), A
	INC	DE
	LD	A, (DE)
	LD	(IX+ST_INDX_HI),A

	INC	DE
	LD	A, (DE)
	OR	A
	JR	Z, SKIP1

SKIP1:
	LD	(IX+ST_DEST_LW), L
	LD	(IX+ST_DEST_HI), H

DEV_RW_LP:
	CALL	SEC_CPY

	PUSH	HL
	LD	L, (IX+ST_INDX_lW)		; INCREMENT SECTOR IDX
	LD	H, (IX+ST_INDX_HI)
	INC	HL
	LD	(IX+ST_INDX_LW), L
	LD	(IX+ST_INDX_HI), H
	POP	HL
skip:

	LD	A, (IX+ST_RD_CNT)	; MORE SECTORS TO GO?
	DEC	A
	LD	(IX+ST_RD_CNT), A
	JR	NZ, DEV_RW_LP

	LD	B, (IX+ST_RD_CND_RQ)	; LOAD SECTORED COPIED COUNT
	XOR	A			; RETURN SUCCESS
	RET

SEC_CPY:
	PUSH	HL			; SAVE DEST ADDR
	LD	DE, SECCNT		; FIND THE BANK FOR SECTOR
	LD	C, (IX+ST_INDX_LW)
	LD	B, (IX+ST_INDX_HI)
	CALL	DIV16

	LD	A, C			; BANK ID TO A
	ADD	A, DRV_BANK_NO
	LD	C, L			; SECTOR OFFSET TO C
	POP	HL			; RESTORE DEST ADDR
	;	A <= BANK FOR SECTOR
	;	C <= SECTOR RELATIVE OFFSET WITHIN BANK (SECTOR MOD SECCNT)

	LD	IX, SEC_RAW_CPY
	JP	CALBNK			; CONTINUE COPY IN OTHER BANK

SEC_RAW_CPY:
	; C IS THE RELATIVE OFFSET WITHIN LOCAL SECTORS

	CALL	MY_GWORK

	CALL	SECTOR_ADDR_IN_BANK
	CALL	COPY_SECTOR_TO_USR
	RET

SECTOR_ADDR_IN_BANK:
	LD	HL, SECSTRT		; AN ARRAY OF 512 SECTORS PER DRIVER BANK
	LD	A, C			; RELATIVE OFFSET
	RLCA				; BC = A * 512
	LD	C, 0
	LD	B, A
	ADD	HL, BC
	RET

	; HL MUST POINT TO SOURCE SECTOR DATA
COPY_SECTOR_TO_USR:
	; dbg.break

	LD	E, (IX+ST_DEST_LW)
	LD	D, (IX+ST_DEST_HI)
	LD	BC, 512
	; HL IS SECTOR SOUND ADDR
	; DE IS DESTINATION ADDR
	LDIR	; COPY TO USER DATA PAGE
	LD	(IX+ST_DEST_HI), D
	RET



;-----------------------------------------------------------------------------
;
; Device information gathering
;
;Input:   A = Device index, 1 to 7
;         B = Information to return:
;             0: Basic information
;             1: Manufacturer name string
;             2: Device name string
;             3: Serial number string
;         HL = Pointer to a buffer in RAM
;Output:  A = Error code:
;             0: Ok
;             1: Device not available or invalid device index
;             2: Information not available, or invalid information index
;         When basic information is requested,
;         buffer filled with the following information:
;
;+0 (1): Numer of logical units, from 1 to 7. 1 if the device has no logical
;        units (which is functionally equivalent to having only one).
;+1 (1): Device flags, always zero in Beta 2.
;
; The strings must be printable ASCII string (ASCII codes 32 to 126),
; left justified and padded with spaces. All the strings are optional,
; if not available, an error must be returned.
; If a string is provided by the device in binary format, it must be reported
; as an hexadecimal, upper-cased string, preceded by the prefix "0x".
; The maximum length for a string is 64 characters;
; if the string is actually longer, the leftmost 64 characters
; should be provided.
;
; In the case of the serial number string, the same rules for the strings
; apply, except that it must be provided right-justified,
; and if it is too long, the rightmost characters must be
; provided, not the leftmost.

DEV_INFO:
	cp	1
	jr	nz, DEV_INFO_2

	xor	a
	LD	DE, DUM_INFO
	LD	BC, DUM_INFO_LN
	LDIR
	ret

DEV_INFO_2:
	ld	a, 1
	ret

;-----------------------------------------------------------------------------
;
; Obtain device status
;
;Input:   A = Device index, 1 to 7
;         B = Logical unit number, 1 to 7
;             0 to return the status of the device itself.
;Output:  A = Status for the specified logical unit,
;             or for the whole device if 0 was specified:
;                0: The device or logical unit is not available, or the
;                   device or logical unit number supplied is invalid.
;                1: The device or logical unit is available and has not
;                   changed since the last status request.
;                2: The device or logical unit is available and has changed
;                   since the last status request
;                   (for devices, the device has been unplugged and a
;                    different device has been plugged which has been
;                    assigned the same device index; for logical units,
;                    the media has been changed).
;                3: The device or logical unit is available, but it is not
;                   possible to determine whether it has been changed
;                   or not since the last status request.
;
; Devices not supporting hot-plugging must always return status value 1.
; Non removable logical units may return values 0 and 1.
;
; The returned status is always relative to the previous invokation of
; DEV_STATUS itself. Please read the Driver Developer Guide for more info.

DEV_STATUS:
	CP	1
	JR	NZ, DEV_STATUS2
	LD	A, B
	CP	0
	JR	Z, DEV_STATUS1
	CP	1
	JR	NZ, DEV_STATUS2

DEV_STATUS1:
	LD	A, 1
	RET

DEV_STATUS2:
	xor	a
	ret


;-----------------------------------------------------------------------------
;
; Obtain logical unit information
;
;Input:   A  = Device index, 1 to 7
;         B  = Logical unit number, 1 to 7
;         HL = Pointer to buffer in RAM.
;Output:  A = 0: Ok, buffer filled with information.
;             1: Error, device or logical unit not available,
;                or device index or logical unit number invalid.
;         On success, buffer filled with the following information:
;
;+0 (1): Medium type:
;        0: Block device
;        1: CD or DVD reader or recorder
;        2-254: Unused. Additional codes may be defined in the future.
;        255: Other
;+1 (2): Sector size, 0 if this information does not apply or is
;        not available.
;+3 (4): Total number of available sectors.
;        0 if this information does not apply or is not available.
;+7 (1): Flags:
;        bit 0: 1 if the medium is removable.
;        bit 1: 1 if the medium is read only. A medium that can dinamically
;               be write protected or write enabled is not considered
;               to be read-only.
;        bit 2: 1 if the LUN is a floppy disk drive.
;+8 (2): Number of cylinders
;+10 (1): Number of heads
;+11 (1): Number of sectors per track
;
; Number of cylinders, heads and sectors apply to hard disks only.
; For other types of device, these fields must be zero.

LUN_INFO:
	CP	1
	JR	NZ, DEV_INFO2
	LD	A, B
	CP	0
	JR	Z, DEV_INFO1
	CP	1
	JR	NZ, DEV_INFO2

DEV_INFO1:
	LD	A, 0			; standard block device
	LD	(HL), 0
	INC	HL

	LD	(HL), 0			; sector size 512
	INC	HL
	LD	(HL), 2
	INC	HL

	LD	(HL), 0
	INC	HL
	LD	(HL), 0
	INC	HL
	LD	(HL), 0
	INC	HL
	LD	(HL), 0
	INC	HL

	LD	(HL), 1			; read only
	INC	HL

	LD	(HL), 0			; cyclinders
	INC	HL
	LD	(HL), 0
	INC	HL

	LD	(HL), 0			; heads
	INC	HL

	LD	(HL), 0			; sectors per track
	RET

DEV_INFO2:
	ld	a,1
	ret

;-----------------------------------------------------------------------------
;
; Print a zero-terminated string on screen
; Input: DE = String address

PRINT:
	ld	a,(de)
	or	a
	ret	z
	call	CHPUT
	inc	de
	jr	PRINT


;-----------------------------------------------------------------------------
;
; Obtain the work area address for the driver
; Input: A=1  to obtain the work area for the master, 2 for the slave
; Preserves A

MY_GWORK:
	PUSH	AF
	XOR	A
	EX	AF,AF'
	XOR	A
	LD	IX, GWORK
	CALL	CALBNK
	POP	AF
	RET


; DIV16     [BC]=[BC]/[DE], remainder in HL.  Preserves DE, IX, IY.

DIV16:
	ld	hl,0
	ld	a,17
div_loop:
	rl	c
	rl	b
	dec	a
	ret	z
;
	adc	hl,hl
	jr	nc,fit_16bit
	or	a		;Remainder exceeds 16 bit
	sbc	hl,de		;This always generates carry
	jr	div_loop
fit_16bit:
	sbc	hl,de
	jr	nc,no_rest
	add	hl,de		;Restore since divisor didn't fit
no_rest:
	ccf			;Prepare quotient bit
	jr	div_loop

;-----------------------------------------------------------------------------
;
; End of the driver code

INFO_S:		db	"MSX Embedded Disk Driver for RC2014", 13, 10, 0

DUM_INFO:	DB	"Embedded Rom Driver", 0
DUM_INFO_LN	EQU	9

; 4000h   +---------------------+
;         |     Page 0 code     |
;         +---------------------+
; 40FEh   |          K          |
;         +---------------------+
; 40FFh   |       Bank ID       |
; 4100h   +---------------------+
;         |                     |
;         |     Additional      |
;         |     driver code     |
;         |                     |
;         |                     |
;         |                     |
; SECSTRT +---------------------+
;         |    sector data 	|
;         |                     |
; 7FD0h   +---------------------+
;         | Bank switching code |
; 7FFFh   +---------------------+

SECTOR_INDX_START	EQU	07F00h
SECTORS			EQU	05F00h		; 8k of data (16 sectors)

	PUBLIC	SECSTRT, SECEND, DATSIZE
DRV_END:

SECSTRT:
	DS	SECCNT * SECSIZ, 0
SECEND:
DATSIZE	EQU	SECCNT * SECSIZ

END:
	end
