
RS232_ID		EQU	8
GET_INFO_TABLE_FN	EQU	0

WRSLT			EQU	00014h
GSLOT1			EQU	0402Dh
CALBNK			EQU	04042h

RS232_JUMP_TABLE	EQU	07BD0h
;-----------------------------------------------------------------------------
;
; Extended BIOS hook.
; Works the expected way, except that it must return
; D'=1 if the old hook must be called, D'=0 otherwise.
; It is entered with D'=1.

	PUBLIC	DRV_EXTBIO
	EXTRN	PRINT

DRV_EXTBIO:
	PUSH	AF
	LD	A, D
	OR	E
	JR	Z, GET_DEVICE_ID

	LD	A, D
	CP	RS232_ID
	JR	NZ, NOT_US			; NOT US

	LD	A, E
	CP	GET_INFO_TABLE_FN
	JR	Z, GET_INFO_TABLE

NOT_US:
	POP	AF
	RET

GET_DEVICE_ID:
	pop	af
	PUSH	DE
	PUSH	BC
	LD	E, RS232_ID
	LD	A, B
	CALL	WRSLT
	INC	HL
	POP	BC
	POP	DE
	RET


GET_INFO_TABLE:
	POP	AF
	PUSH	DE
	PUSH	BC
	XOR	A
	LD	IX, GSLOT1
	CALL	CALBNK		; OUR SLOT IN A
				; REQUEST SLOT IN B
	LD	E, A
	LD	A, B
	CALL	WRSLT
	POP	BC
	PUSH	BC
	INC	HL

	LD	E, LOW RS232_JUMP_TABLE
	LD	A, B
	CALL	WRSLT
	POP	BC
	PUSH	BC
	INC	HL

	LD	E, HIGH RS232_JUMP_TABLE
	LD	A, B
	CALL	WRSLT
	POP	BC
	INC	HL

	POP	DE
	RET

	END
